Container TomTom : Using = "Units;geometries;classifications/tomtom"
{
	container Generates
	{
		parameter<string> Junctions_fss         := 'Ready', ExplicitSuppliers = "FSS/Make_Junctions";
		parameter<string> Streets_fss           := 'Ready', ExplicitSuppliers = "FSS/Make_Streets";
		parameter<string> Streets_fss_selection := 'Ready', ExplicitSuppliers = "Selection/Make_Streets_selection";
	}
	container GPKG
	{
		unit<uint32> Junctions
		: StorageName = "%NetworkModelDataDir%/Infrastructure/TomTom/src/TomTom_Compiled_Themes_2021.gpkg"
		, StorageType = "gdal.vect"
		, StorageReadOnly = "True"
		, DialogData = "BaseProjection" 
		{
			attribute<BaseProjection> geometry;
		}
		unit<uint32> Streets
		: StorageName = "%NetworkModelDataDir%/Infrastructure/TomTom/src/NW2021_SP_streets.gpkg"
		, StorageType = "gdal.vect"
		, StorageReadOnly = "True"
		, DialogData = "BaseProjection" 
		{
			attribute<BaseProjection>    Geometry (arc);
			attribute<FuncRoadClass_EK>  FRC; //Functional Road Class
			attribute<FuncRoadClass>     FRC_rel := (FRC + 1[FuncRoadClass_EK])[FuncRoadClass];
			attribute<float64>           F_JNCTID; //from junction identifier: 
			attribute<float64>           T_JNCTID; //to junction identifier: 
			attribute<float64>           MINUTES; 
			attribute<float64>           METERS; 
			attribute<float64>           KPH; 
			attribute<string>            Oneway; 
		}
	}
	
	container FSS
	{
		unit<uint32> Make_Junctions := GPKG/Junctions
		, StorageName = "%NetworkModelDataDir%/Infrastructure/TomTom/EU/TomTom_Compiled_Themes_2021_Junctions.fss"
		{
			attribute<BaseProjection>     Geometry  := GPKG/Junctions/Geometry;
			attribute<uint64>             JNCTID    := GPKG/Junctions/id[uint64];
		}
	
		unit<uint32> Junctions
		: StorageName = "%NetworkModelDataDir%/Infrastructure/TomTom/EU/TomTom_Compiled_Themes_2021_Junctions.fss"
		, StorageReadOnly = "True"
		{
			attribute<BaseProjection>     Geometry;
			attribute<uint64>             JNCTID;
			
			attribute<bool>               IsWithinStudyArea := IsDefined(point_in_polygon(geometry, Geometries/selection_polygon/polygon));
		}
	
		container Make_Streets
		: StorageName = "%NetworkModelDataDir%/Infrastructure/TomTom/EU/NW2021_SP_streets.fss"
		{
			parameter<uint32>             n                             := #GPKG/Streets;
			attribute<BaseProjection>     Geometry  (GPKG/Streets, arc) := GPKG/Streets/Geometry;
			attribute<FuncRoadClass>      FRC_rel   (GPKG/Streets)      := GPKG/Streets/FRC_rel;
			attribute<uint64>             F_JNCTID  (GPKG/Streets)      := GPKG/Streets/F_JNCTID[uint64];
			attribute<uint64>             T_JNCTID  (GPKG/Streets)      := GPKG/Streets/T_JNCTID[uint64];
			attribute<m>                  METERS    (GPKG/Streets)      := GPKG/Streets/METERS[m];
			attribute<min_f>              MINUTES   (GPKG/Streets)      := GPKG/Streets/MINUTES[min_f];
			attribute<km_hr>              KPH       (GPKG/Streets)      := GPKG/Streets/KPH[km_hr];
			attribute<Directions>         Direction (GPKG/Streets)      := MakeDefined(rlookup(GPKG/Streets/Oneway, Directions/code), 0[Directions]); // relevant voor latere routen (bij direction = 3 moet je f_ en t_ swappen, bij 3 & 4 geldt eenrichtingsverkeer)
		}
		
		container Read_Streets_container
		: StorageName = "%NetworkModelDataDir%/Infrastructure/TomTom/EU/NW2021_SP_streets.fss"
		, StorageReadOnly = "True"
		{
			parameter<uint32> n;
			
			unit<uint32> domain := range(uint32, 0 , n);
			
			attribute<BaseProjection>     Geometry    (domain, arc);
			attribute<FuncRoadClass>      FRC_rel     (domain);
			attribute<uint64>             F_JNCTID    (domain);
			attribute<uint64>             T_JNCTID    (domain);
			
			attribute<km_hr>              KPH         (domain);
			attribute<m>                  METERS      (domain);
			attribute<min_f>              MINUTES     (domain);
			attribute<Directions>         Direction   (domain);
		}
		
		unit<uint32> Streets_src := Read_Streets_container/domain
		{
			attribute<BaseProjection>     Geometry       (arc) := Read_Streets_container/Geometry;
			attribute<BaseProjection>     geometry_clean (arc) := Impl/geometry_clean, IntegrityCheck = "Impl/CleanUp_BeginEndPointLink/Check_ModuloIsEen";
			attribute<FuncRoadClass>      FRC_rel              := Read_Streets_container/FRC_rel;
			
			attribute<uint64>             F_JNCTID_initial     := Read_Streets_container/F_JNCTID;
			attribute<uint64>             T_JNCTID_initial     := Read_Streets_container/T_JNCTID;
			
			attribute<uint64>             F_JNCTID             := Direction == Directions/v/backwards ? T_JNCTID_initial : F_JNCTID_initial; // relevant voor latere routen (bij direction = 3 moet je f_ en t_ swappen, bij 3 & 4 geldt eenrichtingsverkeer) --> Comment van Chris
			attribute<uint64>             T_JNCTID             := Direction == Directions/v/backwards ? F_JNCTID_initial : T_JNCTID_initial; // relevant voor latere routen (bij direction = 3 moet je f_ en t_ swappen, bij 3 & 4 geldt eenrichtingsverkeer) --> Comment van Chris
			
			attribute<km_hr>              KPH            := Read_Streets_container/KPH;
			attribute<m>                  METERS         := Read_Streets_container/METERS;
			attribute<min_f>              MINUTES        := Read_Streets_container/MINUTES;
			attribute<Directions>         Direction      := Read_Streets_container/Direction;

			attribute<Junctions>          F_JNCT_rel     := rlookup(F_JNCTID, Junctions/JNCTID);
			
			container Impl
			{
				attribute<BaseProjection>     geometry_clean        (FSS/Streets_src, arc) := points2sequence(CleanUp_BeginEndPointLink/seq2points/CleanUpPoints/Point, CleanUp_BeginEndPointLink/seq2points/CleanUpPoints/sequence_rel, CleanUp_BeginEndPointLink/seq2points/CleanUpPoints/NewOrdinal);

				container CleanUp_BeginEndPointLink
				{
					//// checks
					// #punten per sequence in org moet oneven zijn.
					parameter<bool> Check_ModuloIsEen           := sum(aantal_punten_per_sequence % 2) == #FSS/Streets_src;
					
					// geen dubbele punten binnen sequence rel
					
					// er mogen geen punten zijn die wel in de org zitten en niet in clean. 
					
					
					attribute<uint32> aantal_punten_per_sequence (FSS/Streets_src) := pcount(seq2points/sequence_rel);
				
					unit<uint32> seq2points := sequence2points(FSS/Streets_src/geometry)
					{
						attribute<Directions> Direction             := rjoin(sequence_rel, id(FSS/Streets_src), FSS/Streets_src/Direction);
						attribute<uint32>     LastOrdinalNr         := rjoin(sequence_rel, id(FSS/Streets_src), last(ordinal, sequence_rel));
						
						attribute<uint32>     LastRelevantOrdinal   := LastOrdinalNr / 2;
						attribute<bool>       IsLastRelevantOrdinal := LastRelevantOrdinal == Ordinal;
						attribute<bool>       IsIrrelevant          := Ordinal > LastRelevantOrdinal; //links are doubled, we only want one-way links.
						
						attribute<uint32>     ReveresedOrdinal      := abs(Ordinal[int32] - LastRelevantOrdinal[int32])[uint32];
						attribute<uint32>     NewOrdinal            := Direction == Directions/v/backwards ? ReveresedOrdinal : Ordinal;

						unit<uint32> CleanUpPoints := select_with_attr_by_cond(seq2points,!seq2points/IsIrrelevant);
					}
				}
			}
		}
	}
	
	container Selection
	{
		attribute<bool>               Streets_IsJunctionWithinStudyArea  (FSS/Streets_src) := FSS/Junctions/IsWithinStudyArea[FSS/Streets_src/F_JNCT_rel];
		attribute<bool>               Streets_IsStreetTypeSubsetSelectie (FSS/Streets_src) := ='FSS/Streets_src/FRC_rel <= '+ModelParameters/StreetTypeSubsetSelectie;
		attribute<bool>               Streets_Selection_Condition        (FSS/Streets_src) := =ModelParameters/UseStreetTypeSubset ? 'Streets_IsStreetTypeSubsetSelectie && Streets_IsJunctionWithinStudyArea' : 'Streets_IsJunctionWithinStudyArea';
		
		unit<uint32> Streets_selection_unit := select_with_attr_by_cond(FSS/Streets_src, Streets_Selection_Condition);
		
		container Make_Streets_selection := Streets_selection_unit
		, StorageName = "=ModelParameters/Roads_path"
		{
			attribute<BaseProjection>     geometry       (Streets_selection_unit, arc) := Streets_selection_unit/geometry_clean;
			attribute<FuncRoadClass>      FRC_rel        (Streets_selection_unit)      := Streets_selection_unit/FRC_rel;
			attribute<uint64>             F_JNCTID       (Streets_selection_unit)      := Streets_selection_unit/F_JNCTID[uint64];
			attribute<uint64>             T_JNCTID       (Streets_selection_unit)      := Streets_selection_unit/T_JNCTID[uint64];
			attribute<m>                  METERS         (Streets_selection_unit)      := Streets_selection_unit/METERS[m];
			attribute<min_f>              MINUTES        (Streets_selection_unit)      := Streets_selection_unit/MINUTES[min_f];
			attribute<km_hr>              KPH            (Streets_selection_unit)      := Streets_selection_unit/KPH[km_hr];
			attribute<Directions>         Direction      (Streets_selection_unit)      := Streets_selection_unit/Direction;
			
			parameter<uint32>             n                                            := #Streets_selection_unit;
		}
		
		container Read_Streets_selection
		: StorageName = "=ModelParameters/Roads_path"
		, StorageReadOnly = "True"
		{
			
			unit<uint32> domain := range(uint32, 0 , n);
			
			attribute<BaseProjection>     Geometry       (domain, arc);
			attribute<FuncRoadClass>      FRC_rel        (domain);
			attribute<uint64>             F_JNCTID       (domain);
			attribute<uint64>             T_JNCTID       (domain);
			
			attribute<m>                  METERS         (domain);
			attribute<min_f>              MINUTES        (domain);
			attribute<km_hr>              KPH            (domain);
			attribute<Directions>         Direction      (domain);
			
			parameter<uint32> n;
		}
	}
	
	unit<uint32> Junctions := FSS/Junctions;
	unit<uint32> Streets   := Selection/Read_Streets_selection/domain
	{
		attribute<BaseProjection>     Geometry       (arc) := Selection/Read_Streets_selection/Geometry;
		attribute<FuncRoadClass>      FRC_rel              := Selection/Read_Streets_selection/FRC_rel;
		attribute<uint64>             F_JNCTID             := Selection/Read_Streets_selection/F_JNCTID;
		attribute<uint64>             T_JNCTID             := Selection/Read_Streets_selection/T_JNCTID;
		
		
		attribute<km_hr>              KPH                  := Selection/Read_Streets_selection/KPH;
		attribute<m>                  METERS               := Selection/Read_Streets_selection/METERS;
		attribute<min_f>              MINUTES              := Selection/Read_Streets_selection/MINUTES;
		attribute<Directions>         Direction            := Selection/Read_Streets_selection/Direction; // relevant voor latere routen (bij direction = 3 moet je f_ en t_ swappen, bij 3 & 4 geldt eenrichtingsverkeer) --> Comment van Chris
		attribute<bool>               IsOneDirection       := Direction == Directions/v/backwards || Direction == Directions/v/forwards;
		attribute<bool>               Connectable          := ='FRC_rel != '+ModelParameters/Street_OD_ConnectabletSelectie+' && !IsOneDirection';
		
		attribute<BaseProjection>     fromPoint            := first_point(geometry);
		attribute<BaseProjection>     toPoint              := last_point(geometry);
		
	}
}

